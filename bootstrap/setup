#!/usr/bin/env zsh

set -eo pipefail

FSOUZA_DOTFILES_CACHE_DIR=${FSOUZA_DOTFILES_CACHE_DIR:-${HOME}/.cache/fsouza-dotfiles}
FSOUZA_DOTFILES_DIR=$(cd "$(dirname $0)/.." && pwd -P)
export FSOUZA_DOTFILES_DIR FSOUZA_DOTFILES_CACHE_DIR

function mklink {
	local source=$1
	local target=$2
	local temp_target=${target}.tmp

	if [ -d ${target} ]; then
		rm -rf ${target}
	elif [ -f ${target} ] && ! [ -L ${target} ]; then
		rm -f ${target}
	fi

	rm -f ${temp_target}
	ln -s ${source} ${temp_target}
	mv -f ${temp_target} ${target}
}

function main {
	local go=${GO:-$(${FSOUZA_DOTFILES_DIR}/bin/__bootstrap_go)}
	env GOBIN=${FSOUZA_DOTFILES_CACHE_DIR}/bin ${go} -C ${FSOUZA_DOTFILES_DIR}/tools install ./...

	mkdir -p ${HOME}/.config

	local home_files=(.editorconfig)
	for file in ${home_files}; do
		mklink ${FSOUZA_DOTFILES_DIR}/${file} ${HOME}/${file}
	done

	local xdg_items=(bat rgrc git)
	for item in ${xdg_items}; do
		mklink ${FSOUZA_DOTFILES_DIR}/${item} ${HOME}/.config/${item}
	done

	local os_prefix=$(uname -s | tr '[:upper:]' '[:lower:]')
	local os_setup_script=${FSOUZA_DOTFILES_DIR}/bootstrap/${os_prefix}-setup
	if [ -x ${os_setup_script} ]; then
		${os_setup_script}
	fi

	if command -v brew &>/dev/null; then
		brew update
		brew bundle --no-lock --file ${FSOUZA_DOTFILES_DIR}/bootstrap/Brewfile
	fi

	if command -v nvim &>/dev/null; then
		env NVIM_DEBUG=1 make -C ${FSOUZA_DOTFILES_DIR}
	fi

	if command -v bat &>/dev/null; then
		bat cache --build
	fi

	tic -x ${FSOUZA_DOTFILES_DIR}/tmux/tmux-256color
}

main
