function _rebind_ctrl-r {
	function fzf-history-widget {
		local selected num
		setopt localoptions noglobsubst noposixbuiltins pipefail no_aliases 2> /dev/null
		selected=( $(fc -rl 1 | perl -ne 'print if !$seen{($_ =~ s/^\s*[0-9]+\s+//r)}++' |
			FZF_DEFAULT_OPTS="--no-mouse --height ${FZF_TMUX_HEIGHT:-40%} ${FZF_DEFAULT_OPTS} -n2..,.. --tiebreak=index --bind=ctrl-r:toggle-sort --expect=ctrl-e $FZF_CTRL_R_OPTS --query=${(qqq)LBUFFER} +m" $(__fzfcmd)) )
		local ret=$?
		if [ -n "${selected}" ]; then
			local accept=0
			if [[ $selected[1] == ctrl-e ]]; then
				accept=1
				shift selected
			fi
			num=$selected[1]
			if [ -n "${num}" ]; then
				zle vi-fetch-history -n $num
				[[ $accept = 0 ]] && zle accept-line
			fi
		fi
		zle reset-prompt
		return $ret
	}
	zle     -N   fzf-history-widget
	bindkey '^R' fzf-history-widget
}

function _setup_fzf {
	if command -v fd &>/dev/null; then
		export FZF_DEFAULT_COMMAND="fd --type f --hidden -E '.git' -E '.hg'"
	fi
	export FZF_DEFAULT_OPTS='--color=light,prompt:#080808,marker:#080808,pointer:#080808,hl:#030303,hl+:#080808,info:#333333,bg+:#bdbda6 --layout default'

	# ensure fzf completion and key-bindings is configured.
	if [[ -v HOMEBREW_PREFIX ]] && [ -d ${HOMEBREW_PREFIX}/opt/fzf ]; then
		[[ $- == *i* ]] && source ${HOMEBREW_PREFIX}/opt/fzf/shell/completion.zsh 2> /dev/null

		source ${HOMEBREW_PREFIX}/opt/fzf/shell/key-bindings.zsh
		_rebind_ctrl-r
	fi
}

if command -v fzf &>/dev/null; then
	_setup_fzf
fi
